apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

// didi plugin
apply plugin: 'com.didi.virtualapk.host'

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        // Enabling multidex support 分包支持
        // multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation" : "$projectDir/schemas".toString()//room的数据库概要、记录
                            , AROUTER_MODULE_NAME : project.getName()]// ARouter 使用的配置
            }
        }
    }

    sourceSets {
        //数据库概要、记录存放位置
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    // signingConfigs必须放到buildTypes之前不然无法找到release和debug信息
    signingConfigs {
        debug {
            storeFile file('sign/sign.jks')
            storePassword '111111'
            keyAlias 'sign'
            keyPassword '111111'
        }
        release {
            storeFile file('sign/sign.jks')
            storePassword '111111'
            keyAlias 'sign'
            keyPassword '111111'
        }
    }

    // 以下多维度支持
    // https://developer.android.google.cn/studio/build/build-variants#flavor-dimensions
    flavorDimensions("default")
    // 以下多渠道支持
    productFlavors {

        // 正式渠道包
        Online {
        }
    }

    // 替换manifestPlaceholders
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    buildTypes {
        release {
            minifyEnabled false // minifyEnabled 混淆处理
            shrinkResources false// shrinkResources 去除无用资源
            signingConfig signingConfigs.release
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "rxDemo_" +
                            "${releaseTime()}_" +
                            "v${defaultConfig.versionName}_" +
                            "${variant.productFlavors[0].name}" +
                            ".apk"
                    outputFileName = fileName
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def dep = rootProject.ext.dependencies
    def testDep = rootProject.ext.testDependencies

    // test
    testImplementation testDep["junit"]
    androidTestImplementation testDep["test-runner"]
    androidTestImplementation testDep["espresso-core"]

    // kotlin
    implementation dep["kotlin-stdlib-jdk7"]


//    'annotationProcessor' dependencies won't be recognized as kapt annotation processors
//    room:compiler
//    lifecycle:compiler
//    arouter-compiler
//    需要使用kapt

    // room
    implementation dep["room"]
    kapt dep["room-compiler"]

    // livedata support
    // ViewModel and LiveData
    implementation dep["lifecycle"]
    kapt dep["lifecycle-compiler"]

    // navigation
    implementation dep["navigation-fragment"]
    implementation dep["navigation-ui"]

    //ARouter compiler
    kapt dep["arouter-compiler"]
    //---------------------------------------------

    // base lib
    implementation project(':baselib')

    if (isLibrary.toBoolean()) {
        // module_a
        implementation project(':module_a')
        // module_b
        implementation project(':module_b')
        // module_c
        implementation project(':module_c')
    }

    // didi plugin core
    implementation 'com.didi.virtualapk:core:0.9.8'

    // 滴滴插件组件通讯lib
    implementation project(":plugin_messenger")
//    implementation(name: 'lib2', ext: 'aar')
}

// enable multidex
//afterEvaluate {
//    tasks.matching {
//        it.name.startsWith('dex')
//    }.each { dx ->
//        if (dx.additionalParameters == null) {
//            dx.additionalParameters = []
//        }
//        dx.additionalParameters += '--multi-dex' // enable multidex
//    }
//}
